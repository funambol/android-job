apply plugin: 'com.android.library'
apply plugin: 'com.github.ben-manes.versions'
apply from: '../build-config/gradle-quality.gradle'

archivesBaseName = 'android-job'

dependencies {
    compileOnly "com.google.android.gms:play-services-gcm:$playServicesVersion"

    //noinspection GradleCompatible,GradleDependency
    api "com.android.support:support-compat:$supportLibVersion"

    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.squareup.assertj:assertj-android:1.1.1'
    androidTestImplementation "com.google.android.gms:play-services-gcm:$playServicesVersion"

    androidTestImplementation "com.facebook.stetho:stetho:$stethoVersion"
    androidTestImplementation "com.android.support:support-v4:$supportLibVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    resourcePrefix 'job_'

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)

        consumerProguardFiles 'proguard.cfg'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError true
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
        }
    }
}


ext {
    groupId = 'com.funambol'
    versionId = '1.2.7'
    gitProjectUrl = "https://github.com/funambol/android-job"
    gitUrl = "https://github.com/funambol/android-job"
}

group = groupId  // Maven Group ID for the artifact
version = versionId

apply plugin: 'com.github.dcendents.android-maven'
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId groupId
                artifactId "android-job"

                // Add your description here
                name "android-job"
                description ""
                url gitProjectUrl
                scm {
                    connection gitUrl
                    url gitProjectUrl
                }
            }
        }
    }
}


apply from: '../build-config/gradle-push.gradle'
